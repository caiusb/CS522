mod IMP-CONFIGURATIONS-SMALLSTEP is including IMP-DESUGARED-SYNTAX + STATE .

  including BUFFER .

  sorts Configuration ExtendedConfiguration .
  subsort Configuration < ExtendedConfiguration .
  op <_,_> : AExp State -> Configuration .
  op <_,_> : BExp State -> Configuration .
  op <_,_> : Stmt State -> Configuration .
  op <_> : Pgm -> Configuration .
  op < error > : -> Configuration . 			       --- error
  op o_ : Configuration -> ExtendedConfiguration [prec 80] .   --- one step
  op *_ : Configuration -> ExtendedConfiguration [prec 80] .   --- all steps
  var Cfg Cfg' : Configuration .
 crl * Cfg => * Cfg' if o Cfg => Cfg' .
endm

mod IMP-SEMANTICS-SMALLSTEP is including IMP-CONFIGURATIONS-SMALLSTEP .
  var X : Id .  var Sigma Sigma' : State .  var I I1 I2 : Int .  var Xl : List{Id} .
  var A A' A1 A1' A2 A2' : AExp .  var B B' B1 B1' B2 : BExp .  var S S' S1 S1' S2 : Stmt .
  var P : Pgm .

--- lookup
 crl o < X,Sigma > => < Sigma(X),Sigma > if Sigma(X) =/=Bool undefined .

--- add
 crl o < A1 + A2,Sigma > => < A1' + A2,Sigma > if o < A1,Sigma > => < A1',Sigma > .
 crl o < A1 + A2,Sigma > => < A1 + A2',Sigma > if o < A2,Sigma > => < A2',Sigma > .
  rl o < I1 + I2,Sigma > => < I1 +Int I2,Sigma > .
 crl o < A1 + A2,Sigma > => < error > if o < A1,Sigma > => < error > .
 crl o < A1 + A2,Sigma > => < error > if o < A2,Sigma > => < error > .

--- div
 crl o < A1 / A2,Sigma > => < A1' / A2,Sigma > if o < A1,Sigma > => < A1',Sigma > .
 crl o < A1 / A2,Sigma > => < A1 / A2',Sigma > if o < A2,Sigma > => < A2',Sigma > .
 crl o < I1 / I2,Sigma > => < I1 /Int I2,Sigma > if I2 =/=Bool 0 .
 crl o < A1 / A2,Sigma > => < error > if o < A1,Sigma > => < error > .
 crl o < A1 / A2,Sigma > => < error > if o < A2,Sigma > => < error > .
 crl o < I1 / I2,Sigma > => < error > if I2 ==Bool 0 .


--- less than
 crl o < A1 <= A2,Sigma > => < A1' <= A2,Sigma > if o < A1,Sigma > => < A1',Sigma > .
 crl o < I1 <= A2,Sigma > => < I1 <= A2',Sigma > if o < A2,Sigma > => < A2',Sigma > .
  rl o < I1 <= I2,Sigma > => < I1 <=Int I2,Sigma > .
  crl o < A1 <= A2,Sigma > => < error > if o < A1,Sigma > => < error > .
  crl o < A1 <= A2,Sigma > => < error > if o < A2,Sigma > => < error > .

--- negation
 crl o < ! B,Sigma > => < ! B',Sigma > if o < B,Sigma > => < B',Sigma > .
  rl o < ! true,Sigma > => < false,Sigma > .
  rl o < ! false,Sigma > => < true,Sigma > .
 crl o < ! B,Sigma > => < error > if o < B,Sigma > => < error > .

--- and
 crl o < B1 && B2,Sigma > => < B1' && B2,Sigma > if o < B1,Sigma > => < B1',Sigma > .
  rl o < false && B2,Sigma > => < false,Sigma > .
  rl o < true && B2,Sigma > => < B2,Sigma > .
 crl o < B1 && B2,Sigma > => < error > if o < B1,Sigma > => < error > .
 crl o < true && B2,Sigma > => < error > if o < B2,Sigma > => < error > .

--- block
  rl o < {S},Sigma > => < S,Sigma > .

--- assignment
 crl o < X = A ;,Sigma > => < X = A' ;,Sigma > if o < A,Sigma > => < A',Sigma > .
 crl o < X = I ;,Sigma > => < {},Sigma[I / X] > if Sigma(X) =/=Bool undefined .
 crl o < X = A ;,Sigma > => < error > if o < A,Sigma > => < error > .

--- statement concat
 crl o < S1 S2,Sigma > => < S1' S2,Sigma' > if o < S1,Sigma > => < S1',Sigma' > .
  rl o < {} S2,Sigma > => < S2,Sigma > .
 crl o < S1 S2,Sigma > => < error > if o < S1,Sigma > => < error > .

--- if
 crl o < if (B) S1 else S2,Sigma > => < if (B') S1 else S2,Sigma >
  if o < B,Sigma > => < B',Sigma  > .
  rl o < if (true) S1 else S2,Sigma > => < S1,Sigma > .
  rl o < if (false) S1 else S2,Sigma > => < S2,Sigma > .
 crl o < if (B) S1 else S2,Sigma > => < error > if o < B,Sigma > => < error > .

--- while
  rl o < while (B) S,Sigma > => < if (B) {S while (B) S} else {},Sigma > .

--- program
  rl o < P > => < P, .State > .
  crl o < let X = A in S,Sigma > => < let X = A' in S,Sigma > if o < A,Sigma > => < A',Sigma > .
  crl o < let X = I in S,Sigma > => < let X = Sigma'(X) in S',Sigma'[Sigma(X) / X] >
   if o < S,Sigma[I / X] > => < S',Sigma' > .
   rl o < let X = I in {},Sigma > => < {},Sigma > .
  crl o < let X = A in S,Sigma > => < error > if o < A,Sigma > => < error > .
  crl o < let X = I in S,Sigma > => < error >
   if o < S,Sigma[I / X] > => < error > .

endm
