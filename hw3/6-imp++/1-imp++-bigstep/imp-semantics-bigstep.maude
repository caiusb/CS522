mod IMP-CONFIGURATIONS-BIGSTEP is including IMP-SYNTAX + STATE .

  sort Configuration .
  op <_,_> : AExp State -> Configuration .
  op <_,_> : BExp State -> Configuration .
  op <_,_> : Stmt State -> Configuration .
  op   <_> : State -> Configuration .
  op   <_> : Pgm -> Configuration .

  ---for halting
  op <`error`> : -> Configuration .
  op <`halting`,_> : State -> Configuration .
endm

mod IMP-SEMANTICS-BIGSTEP is including IMP-CONFIGURATIONS-BIGSTEP .
  var X : Id .  var Xl : List{Id} .  var Sigma Sigma' Sigma'' Sigma1 Sigma2 : State .
  var I I1 I2 : Int .  var T : Bool .
  var A A1 A2 : AExp .  var B B1 B2 : BExp .  var S S1 S2 : Stmt .

 crl < X,Sigma > => < Sigma(X),Sigma >
  if Sigma(X) =/=Bool undefined .
 crl < A1 + A2,Sigma > => < I1 +Int I2,Sigma2 > 
  if < A1,Sigma > => < I1,Sigma1 > /\ < A2,Sigma1 > => < I2,Sigma2 > .
 crl < A1 + A2,Sigma > => < I1 +Int I2,Sigma1 >
  if < A2,Sigma > => < I2,Sigma2 > /\ < A1,Sigma2 > => < I1,Sigma1 > .
 crl < A1 + A2,Sigma > => < error >
  if < A1,Sigma > => < error > .
 crl < A1 + A2,Sigma > => < error >
  if < A2,Sigma > => < error > .

 crl < A1 / A2,Sigma > => < I1 /Int I2,Sigma2 >
  if < A1,Sigma > => < I1,Sigma1 > /\ < A2,Sigma1 > => < I2,Sigma2 > /\ I2 =/=Bool 0 .
 crl < A1 / A2,Sigma > => < I1 /Int I2,Sigma1 >
  if < A2,Sigma > => < I2,Sigma2 > /\ < A1,Sigma2 > => < I1,Sigma1 > /\ I2 =/=Bool 0 .
 crl < A1 / A2,Sigma > => < error >
  if < A1,Sigma > => < error > .
 crl < A1 / A2,Sigma > => < error >
  if < A2,Sigma > => < error > .
 crl < A1 / A2,Sigma > => < error >
  if < A2,Sigma > => < 0,Sigma' > .

 crl < A1 <= A2,Sigma > => < I1 <=Int I2,Sigma2 > 
  if < A1,Sigma > => < I1,Sigma1 > /\ < A2,Sigma1 > => < I2,Sigma2 > .
 crl < A1 <= A2,Sigma > => < error >
  if < A1,Sigma > => < error > .
 crl < A1 <= A2,Sigma > => < error >
  if < A2,Sigma > => < error > .

 crl < ! B,Sigma > => < false,Sigma' > 
  if < B,Sigma > => <  true,Sigma' > .
 crl < ! B,Sigma > => <  true,Sigma' > 
  if < B,Sigma > => < false,Sigma' > .
 crl < ! B,Sigma > => < error >
  if < B,Sigma > => < error > .

 crl < B1 && B2,Sigma > => < false,Sigma1 > 
  if < B1,Sigma > => < false,Sigma1 > .
 crl < B1 && B2,Sigma > => < T,Sigma2 > 
  if < B1,Sigma > => < true,Sigma1 > /\ < B2,Sigma1 > => < T,Sigma2 > .
 crl < B1 && B2,Sigma > => < error >
  if < B1,Sigma > => < error > .
 crl < B1 && B2,Sigma > => < error >
  if < B1,Sigma > => < true,Sigma' > /\ < B2,Sigma > => < error > .

  rl < {},Sigma > => < Sigma > .
 crl < {S},Sigma > => < Sigma' >
  if < S,Sigma > => < Sigma' > .
 crl < X = A ;,Sigma > => < Sigma'[I / X] >
  if < A,Sigma > => < I,Sigma' > /\ Sigma(X) =/=Bool undefined .
 crl < X = A ;,Sigma > => < error >
  if < A,Sigma > => < error > .

 crl < S1 S2,Sigma > => < Sigma2 >
  if < S1,Sigma > => < Sigma1 > /\ < S2,Sigma1 > => < Sigma2 > .
 crl < if (B) S1 else S2,Sigma > => < Sigma1 > 
  if < B,Sigma > => < true,Sigma'  > /\ < S1,Sigma' > => < Sigma1 > .
 crl < if (B) S1 else S2,Sigma > => < Sigma2 > 
  if < B,Sigma > => < false,Sigma' > /\ < S2,Sigma' > => < Sigma2 > .
 crl < while (B) S,Sigma > => < Sigma' >
  if < B,Sigma > => < false,Sigma' > .
 crl < while (B) S,Sigma > => < Sigma'' > 
  if < B,Sigma > => < true,Sigma' > /\ < S while (B) S,Sigma' > => < Sigma'' > .

 crl < int Xl ; S > => < Sigma >
  if < S,(Xl |-> 0) > => < Sigma > .

 crl < ++ X,Sigma > => < Sigma(X) +Int 1,Sigma[(Sigma(X) +Int 1) / X] >
  if Sigma(X) =/=Bool undefined .

endm
